plugins {
    id "com.gradle.plugin-publish" version "0.10.0"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'ivy-publish'
    id 'maven'
    id "org.sonarqube" version "2.6"
}


if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}


repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

group = 'com.github.arekbee'
version = '0.4'
def pluginName = 'gradle-R-plugin'
def pluginDescription = 'Gradle R plugin for R projects and packages'
def website = 'https://github.com/arekbee/gradle-R-plugin'
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar, sourcesJar
}




gradlePlugin {
    plugins {
        rPlugin  {
            id = "com.github.arekbee.${pluginName}"
            implementationClass = 'org.arekbee.RPlugin'
        }
    }
}

pluginBundle {
    website = website
    vcsUrl = 'https://github.com/arekbee/gradle-R-plugin'
    description = pluginDescription
    tags = ['R', 'RStudio', pluginName ]

    plugins {
        rPlugin  {
            displayName = 'Gradle R Plugin'
            tags = ['R', 'devtools', 'packrat', 'RCode', 'RExec']
            version = version
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}


publishing {
    repositories {
        maven {
            url '../../consuming/maven-repo'

        }
        ivy {
            url '../../consuming/ivy-repo'
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = pluginName
            version = version

            from components.java
        }
    }
}



uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name pluginName
                packaging 'jar'
                // optionally artifactId can be defined here
                description pluginDescription
                url website

                scm {
                    connection website
                    developerConnection website
                    url website
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/arekbee/gradle-R-plugin/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'arekbee'
                        name = 'arekbee'
                        email 'arekbee@gmail.com'
                    }
                }
            }
        }
    }
}

task RunIt {
    dependsOn check
    dependsOn build
    dependsOn publish
    dependsOn test
    dependsOn uploadArchives
    
}
